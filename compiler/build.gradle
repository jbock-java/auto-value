plugins {
    id('java-library')
    id('maven-publish')
    id('com.github.johnrengelman.shadow') version('7.1.2')
    id('signing')
}

group = 'io.github.jbock-java'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.compilerArgs << '--release'
    options.compilerArgs << '11'
}

javadoc {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

shadowJar {
    minimize()
    archiveClassifier.set('')
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

shadowJar {
    minimize()
    archiveClassifier.set('')
    relocate('io.jbock.auto.common', 'io.jbock.auto.value.shadow.auto.common')
    relocate('io.jbock.javapoet', 'io.jbock.auto.value.shadow.javapoet')
}

dependencies {
    def annotations = 'io.github.jbock-java:auto-value-annotations:1.8.2.1'
    shadow(annotations)
    implementation('io.github.jbock-java:auto-common:1.2.3')
    implementation('io.github.jbock-java:javapoet:1.15')
    testImplementation('com.google.errorprone:error_prone_type_annotations:2.10.0')
    testImplementation('com.google.guava:guava-testlib:31.0.1-jre')
    testImplementation('org.checkerframework:checker-qual:3.19.0')
    testImplementation('org.apache.velocity:velocity:1.7')
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.1')
    testImplementation('org.junit.vintage:junit-vintage-engine:5.8.1')
    testImplementation('io.github.jbock-java:compile-testing:0.19.0')
    testImplementation('io.github.jbock-java:truth:1.1.3.0')
    testImplementation('org.mockito:mockito-core:4.0.0')
    testImplementation('org.hamcrest:hamcrest-core:2.2')
    testImplementation(annotations)
}

jar {
    manifest {
        attributes(
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events('failed')
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

artifacts {
    archives(javadocJar, sourcesJar)
}

// https://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'auto-value'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'AutoValue Processor'
                packaging = 'jar'
                description = 'Immutable value-type code generation for Java 1.6+.'
                url = 'https://github.com/jbock-java/auto-value'

                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Various'
                        name = 'Various'
                        email = 'jbock-java@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jbock-java/auto-value.git'
                    developerConnection = 'scm:git:https://github.com/jbock-java/auto-value.git'
                    url = 'https://github.com/jbock-java/auto-value'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
            }
        }
    }
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
